**Sokolova, A., Buskens, V., Raub, W. Cooperation Through Rational Investments in Social Organization*

version 16
clear all
cls
set more off
cd "\\soliscom.uu.nl\users\Buske101\My Documents\Data\Anna\Data_code Anna"
// original data
use inv-paper_original-data.dta, clear

//////////////////////////// Data preparation ///////////////////////////////



//delete the 1 participant who left
drop if session=="200211_0947"&Subject==6

//Generate investment cost variable:
gen inv_cost = 5
replace inv_cost = 15 if session=="191211_0939" | session=="191211_1339" ///
| session=="191212_1126" | session=="200115_0953" | session=="200117_0947" ///
| session=="200211_0947"
replace inv_cost = 45 if session=="191210_1019" | session=="191210_1447" ///
| session=="200205_1002"

//Generate condition variable:
gen exp_cond = 0
/// RI_OS_lc (start with 'Repeated interactions/One-shot PD', low investment costs) 
replace exp_cond = 1 if inv_cost==5 & RepeatFirst==1
/// CA_OS_lc (start with 'Contractual agreements/One-shot PD', low investment costs)
replace exp_cond = 2 if inv_cost==5 & RepeatFirst==0
/// RI_OS_mc
replace exp_cond = 3 if inv_cost==15 & RepeatFirst==1
/// CA_OS_mc
replace exp_cond = 4 if inv_cost==15 & RepeatFirst==0
/// RI_OS_hc
replace exp_cond = 5 if inv_cost==45 & RepeatFirst==1
/// CA_OS_hc
replace exp_cond = 6 if inv_cost==45 & RepeatFirst==0

//Generate session size variable
gen session_size = 12
replace session_size = 20 if session=="191209_0848" | session=="191210_1447"
replace session_size = 22 if session=="191205_1445"
replace session_size = 16 if session=="191210_1019" | session=="191211_1339"
replace session_size = 14 if session=="200205_1002" 
replace session_size = 4 if session=="200113_0944" 
replace session_size = 10 if session=="200211_0947" | session=="191212_1126"
replace session_size = 8 if session=="191211_0939" 

// Generate a unique ID for subjects and sessions
encode session, generate (session_id)
gen subject_id = (session_id * 100) + Subject

// Part 3 dummy
gen part3 = 0 if Treatment < 3
replace part3 = 1 if Treatment > 2
replace part3 = . if Treatment > 4
gen part2 = 1-part3

save "inv-paper_prepared-data.dta", replace


///////////////////////// Investments in embeddedness /////////////////////////
///Table 1:
// inv_cost is the investment costs ( 5 15 45 )
// part3 is 1 for the part3 (second game)
// exp_cond is 1: low_cost, repeat first; 2: low cost, repeat second; 3: medium cost, repeat first etc.
// iter: iteration within part 

bys inv_cost exp_cond: tab session if iter==1 & part3 == 1 & Period==1

/// Table 2:
bys part3: tab choice exp_cond if (part3==0 | part3==1) & Period==1, col

// Number of Periods in RI, randomization for the whole session
// footnote on duration of repeated game
egen Periods = max(Period), by(session_id Treatment)
tab Periods Treatment if Period==1 & Treatment<7
* histogram Periods if Period==1 & Treatment<7 & Subject==1, start(1) freq xtick(1(1)14) discrete

/// Earnings from games 
egen payoff_average = mean(Payoff), by(session Treatment Subject)
gen payoff_full = PayoffAverage
replace payoff_full = payoff_average if condition==2
gen payoffwithcost = payoff_full
replace payoff_full = payoff_full + inv_cost if condition != 1


gen payoff_full2 = payoff_full
gen payoff_full3 = payoff_full
gen payoff_ca_full = payoff_full
gen payoff_ri_full = payoff_full

replace payoff_full2 = . if Treatment > 2 | Period>1
replace payoff_full3 = . if (Treatment < 3 | Treatment > 4) | Period > 1 
replace payoff_ca_full = . if condition!=3 | Treatment > 4 | Period > 1
replace payoff_ri_full = . if condition!=2 | Treatment > 4 | Period > 1


sort subject_id
by subject_id: egen payoff_p2 = total(payoff_full2)
by subject_id: egen payoff_p3 = total(payoff_full3)
by subject_id: egen payoff_ca = total(payoff_ca_full)
by subject_id: egen payoff_ri = total(payoff_ri_full)

sort session Treatment subject
//centered (standardized) payoffs from Part 2
egen mean_payoff_p2 = mean(payoff_p2)
gen payoff_p2_cntr = payoff_p2 - mean_payoff_p2
replace payoff_p2_cntr = 0 if Treatment < 3
replace payoff_p2_cntr = . if Treatment > 4

/// Investments in cooperation mechanisms in Part 2 / 3
gen choice_mech = (condition>1)
gen condition_p2 = 1 if condition > 1
replace condition_p2 = 0 if condition==1
replace condition_p2 = .  if Treatment > 2 | Period>1

gen condition_p3 = 1 if condition > 1
replace condition_p3 = 0 if condition==1
replace condition_p3 = .  if (Treatment < 3 | Treatment > 4) | Period>1

gen condition_ri = (condition==2)
gen condition_ca = (condition==3)
replace condition_ri = .  if Treatment > 4 | Period>1
replace condition_ca = .  if Treatment > 4 | Period>1

sort subject_id
by subject_id: egen mechexp_p2 = total(condition_p2)
by subject_id: egen mechexp_p3 = total(condition_p3)
by subject_id: egen mechexp_ri = total(condition_ri)
by subject_id: egen mechexp_ca = total(condition_ca)

gen payoff_av_p2 = payoff_p2/2
gen payoff_av_p3 = payoff_p2/3
gen payoff_av_ca = payoff_ca/mechexp_ca
gen payoff_av_ri = payoff_ri/mechexp_ri
replace payoff_av_ca = 0 if payoff_av_ca == .
replace payoff_av_ri = 0 if payoff_av_ri == .


sort session Treatment subject

//centered (standardized) investments in mechanisms from Part 2
egen mean_mechexp_p2 = mean(mechexp_p2)
gen mechexp_p2_cntr = mechexp_p2 - mean_mechexp_p2
replace mechexp_p2_cntr = 0 if Treatment < 3
replace mechexp_p2_cntr = . if Treatment > 4

// choice of RI
gen choice_RI = 1 if choice==1 & RepeatFirst==1 & Treatment < 3
replace choice_RI = 1 if choice==1 & RepeatFirst==0 & Treatment > 2
replace choice_RI = 0 if choice==0 & RepeatFirst==1 & Treatment < 3
replace choice_RI = 0 if choice==0 & RepeatFirst==0 & Treatment > 2
replace choice_RI = . if Treatment > 4

// choice of CA
gen choice_CA = 1 if choice==1 & RepeatFirst==0 & Treatment < 3
replace choice_CA = 1 if choice==1 & RepeatFirst==1 & Treatment > 2
replace choice_CA = 0 if choice==0 & RepeatFirst==0 & Treatment < 3
replace choice_CA = 0 if choice==0 & RepeatFirst==1 & Treatment > 2
replace choice_CA = . if Treatment > 4


//whether the choice is between CA(contractual agreements) and OS(one-shot) (0) 
///or RI(repeated interactions) and OS (1) - 
gen ri_os = 0
replace ri_os = 1 if exp_cond==1 & Treatment<3
replace ri_os = 1 if exp_cond==3 & Treatment<3
replace ri_os = 1 if exp_cond==5 & Treatment<3
replace ri_os = 1 if exp_cond==2 & Treatment>2
replace ri_os = 1 if exp_cond==4 & Treatment>2
replace ri_os = 1 if exp_cond==6 & Treatment>2
replace ri_os = . if Treatment > 4

gen ca_os = 1 - ri_os

//table payoffs by costs / type of arrangement in Parts 2 and 3
bys condition ri_os: tabstat payoffwithcost if Period==1 & Treatment<5, by(inv_cost) stats (mean N)


drop if Period>1

gen ca_low = (inv_cost==5)*(ca_os)
gen ca_med = (inv_cost==15)*(ca_os)
gen ca_high = (inv_cost==45)*(ca_os)

gen ri_med = (inv_cost==15)*(ri_os)
gen ri_high = (inv_cost==45)*(ri_os)

// Table 3 in paper
melogit choice ri_med ri_high ca_low ca_med ca_high || subject_id:
melogit choice i.inv_cost##i.ca_os
margins, over(ca_os inv_cost)
margins, over(ca_os inv_cost) pwcompare(effects)

melogit choice i.exp_cond if Treatment < 5 || subject_id:


// robustness checks on clustering elements
// with session level control random effect does not estimate well, 0-estimate for session level
melogit choice i.inv_cost##i.ca_os || session_id: || subject_id:
margins, over(inv_cost ca_os)
margins, over(inv_cost ca_os) pwcompare(effects)

// with only 
// only session id random effects estimates random effect at that level at 0, 
melogit choice i.inv_cost##i.ca_os || session_id:
xtlogit choice i.inv_cost##i.ca_os, i(session_id)
// with session clustering  still same results
logit choice i.inv_cost##i.ca_os, cluster(session_id)
logit choice i.inv_cost##i.ca_os, cluster(subject_id)


/////////////////////////////// RI vs CA //////////////////////////////////////

///// Part 4


*sort session Treatment subject
*egen mean_payoff_ri = mean(payoff_ri_full)
*gen payoff_ri_cntr = payoff_ri_full - mean_payoff_ri
*egen mean_payoff_ca = mean(payoff_ca_full)
*gen payoff_ca_cntr = payoff_ca_full - mean_payoff_ca


*sort subject_id
*by subject_id: egen ri_p23_full = total(ri_p23)
*by subject_id: egen ca_p23_full = total(ca_p23)
*sort session Treatment subject
*egen mean_ri = mean(ri_p23_full)
*gen ri_p23_cntr = ri_p23_full - mean_ri
*egen mean_ca = mean(ca_p23_full)
*gen ca_p23_cntr = ca_p23_full - mean_ca

//empty model: constant (Table 4, Model 1)
melogit choice if Treatment > 4 || subject_id:
margins

//model with investment costs (Table 4, Model 2)
melogit choice i.inv_cost if Treatment > 4 || subject_id:
margins, over(inv_cost)

// additional tests
gen inv_cost5 = (inv_cost == 5)
gen inv_cost15 = (inv_cost == 15)
gen inv_cost45 = (inv_cost == 45)
melogit choice inv_cost15 inv_cost45 if Treatment > 4 || subject_id:
melogit choice inv_cost5 inv_cost45 if Treatment > 4 || subject_id:
melogit choice inv_cost5 inv_cost15 if Treatment > 4 || subject_id:

//robustness
melogit choice i.inv_cost if Treatment > 4 || session_id: || subject_id:
logit choice i.inv_cost if Treatment > 4, cluster(session_id)

// Table D.5
//model with previous average earnings
melogit choice i.inv_cost c.payoff_av_ri c.payoff_av_ca if Treatment > 4 || subject_id:

// model with previous choices only
melogit choice i.inv_cost mechexp_ri mechexp_ca if Treatment>4 || subject_id:

//model with previous earnings and previous choices
melogit choice i.inv_cost c.payoff_av_ri c.payoff_av_ca mechexp_ri mechexp_ca if Treatment>4 || subject_id: 


//////////////////////////// Effects of RI and CA ////////////////////////////

//Recode decision
///0 - defection, 1 - cooperation
gen decision_coop = 0
replace decision_coop = 1 if decision==0
replace decision_coop = . if Treatment==7


///// Parts 2-4 

***decision_coop = 1: 'cooperation'
// Table 5:
tab condition decision_coop if part2==1, row
tab condition decision_coop if part3==1, row
tab condition decision_coop if Treatment > 4, row

//intercept only
melogit decision_coop || subject_id:
estat ic
estat icc

// Table 6
//model only with mechanism types (Table 6)
melogit decision_coop i.condition || subject_id:
estimates store coop_mech
estat ic
estat icc
margins, over(condition)

// analyses separately by part give same results
melogit decision_coop i.condition  if part2==1|| subject_id:
melogit decision_coop i.condition  if part3==1|| subject_id:
melogit decision_coop i.condition  if Treatment > 4|| subject_id:



///////////////////////////// APPENDIX /////////////////////////////

/// Table D1: investments in Part 2
melogit choice i.ca_os if part3 == 0 || subject_id:

melogit choice ri_med ri_high ca_low ca_med ca_high if part3 ==0 || subject_id:
melogit choice i.inv_cost##i.ca_os if part3 == 0 || subject_id: 
margins, over(inv_cost ca_os)
margins, over(inv_cost ca_os) pwcompare(effects)

///+Part 2, iteration 1
logit choice i.inv_cost i.ca_os i.inv_cost##i.ca_os if part3 == 0 & iteration == 1

/// Table D2: effects on cooperation in Part 2 
melogit decision_coop i.condition if part3 == 0 || subject_id:

		

///+Part 2, iteration 1
logit decision_coop i.condition i.inv_cost i.inv_cost##i.choice_mech if part3 == 0 & iteration == 1


/// Table D3: investements in CA and RI, full table (Parts 2,3)

*********** Mlvl logreg, random intercept on a subject lvl: RI ***********
//intercept only
melogit choice_RI || subject_id:
estat ic
estimates store intercept_ri

//simple model
meqrlogit choice_RI i.inv_cost || subject_id: , var
melogit choice_RI i.inv_cost || subject_id:
margins i.inv_cost, pwcompare (effects)  

//experience
melogit choice_RI i.inv_cost i.part3 c.payoff_p2_cntr c.mechexp_p2_cntr || subject_id:

// Table D.3
//experience+interaction
melogit choice_RI i.inv_cost i.part3  c.payoff_p2_cntr c.mechexp_p2_cntr ///
c.payoff_p2_cntr#c.mechexp_p2_cntr || subject_id:
margins i.inv_cost, pwcompare (effects)  


*********** Mlvl logreg, random intercept on a subject lvl: CA ***********
//intercept only
melogit choice_CA || subject_id:
estat ic
estat icc

//simple model
melogit choice_CA i.inv_cost || subject_id:
meqrlogit choice_CA i.inv_cost || subject_id: , var

//model with experience
melogit choice_CA i.inv_cost i.part3 c.payoff_p2_cntr c.mechexp_p2_cntr ///
|| subject_id:
margins i.inv_cost, pwcompare (effects)  


// Table D.3 model 2
//experience+interaction
melogit choice_CA i.inv_cost i.part3  c.payoff_p2_cntr c.mechexp_p2_cntr ///
c.payoff_p2_cntr#c.mechexp_p2_cntr || subject_id:



/// Table D4:

//// Session characteristics
// session size - session_size
// order condition - RepeatFirst

//// Social value orientations
alpha trust1 trust2 trust3 if Period==1 & Treatment==1, item
alpha risk1 risk1_a risk1_b risk1_c risk1_d risk1_e risk1_f if Period==1 & Treatment==1, item
gen reciprocity3_r = 10-reciprocity3
alpha reciprocity1 reciprocity2 reciprocity3_r reciprocity4 if Period==1 & Treatment==1, item

// drop reciprocity3, does not fit
gen trust = (trust1+trust2+trust3)/3
gen risk = (risk1+risk1_a+risk1_b+risk1_c+risk1_d+risk1_e+risk1_f)/7
gen reciprocity = (reciprocity1+reciprocity2+reciprocity4)/3

alpha trust1 trust2 trust3 if Period==1 & Treatment==1, item
alpha risk1 risk1_a risk1_b risk1_c risk1_d risk1_e risk1_f if Period==1 & Treatment==1, item
alpha reciprocity1 reciprocity2 reciprocity3_r reciprocity4 if Period==1 & Treatment==1, item

//// SVO slider measure
//svo_type (Altruistic - 1, Prosocial - 2, Individualistic - 3, Competitive - 4)

sort subject_id Treatment
replace svo_type = svo_type[_n+6] if Treatment == 1
replace svo_type = svo_type[_n+5] if Treatment == 2
replace svo_type = svo_type[_n+4] if Treatment == 3
replace svo_type = svo_type[_n+3] if Treatment == 4
replace svo_type = svo_type[_n+2] if Treatment == 5
replace svo_type = svo_type[_n+1] if Treatment == 6

replace svo_angle = svo_angle[_n+6] if Treatment == 1
replace svo_angle = svo_angle[_n+5] if Treatment == 2
replace svo_angle = svo_angle[_n+4] if Treatment == 3
replace svo_angle = svo_angle[_n+3] if Treatment == 4
replace svo_angle = svo_angle[_n+2] if Treatment == 5
replace svo_angle = svo_angle[_n+1] if Treatment == 6

gen prosocial = (svo_type == 2)


//// Demographic and social characteristics
// age
//nationality
gen international = 1 
replace international = 0 if nationality=="Dutch"
replace international = 0 if nationality=="dutch"
replace international = 0 if nationality=="NL"
replace international = 0 if nationality=="nl"

// gender
gen gender_num = 0 if gender=="Female"
replace gender_num = 1 if gender=="Male"

// game_theory - knowledge in game theory
gen gt_knowledge = 0 if game_theory=="No;"
replace gt_knowledge = 1 if game_theory!="No;"

// experience - experimental participation
gen lab_experience = 0 if experience=="No"
replace lab_experience = 1 if experience=="Yes, once or twice"
replace lab_experience = 2 if experience=="Yes, more than two times but less than five"
replace lab_experience = 3 if experience=="Yes, five times or more"

// understanding the instructions - exp1 (0-9, not difficult - very diffcult)

//Table D.4
/// RC: demographics 
meqrlogit choice_RI i.inv_cost ///
c.age i.gender_num i.international || subject_id: , var

meqrlogit choice_CA i.inv_cost ///
c.age i.gender_num i.international || subject_id: , var

meqrlogit decision_coop i.condition i.inv_cost ///
c.age i.gender_num i.international || subject_id: , var

/// RC: social orientations 
meqrlogit choice_RI i.inv_cost ///
c.trust c.risk c.reciprocity i.prosocial || subject_id: , var

meqrlogit choice_CA i.inv_cost ///
c.trust c.risk c.reciprocity i.prosocial || subject_id: , var

meqrlogit decision_coop i.condition i.inv_cost ///
c.trust c.risk c.reciprocity i.prosocial || subject_id: , var

/// RC: social orientations (extra analysis not included in the table)
meqrlogit choice_RI i.inv_cost ///
c.trust c.risk c.reciprocity svo_angle || subject_id: , var

meqrlogit choice_CA i.inv_cost ///
c.trust c.risk c.reciprocity svo_angle || subject_id: , var

meqrlogit decision_coop i.condition i.inv_cost ///
c.trust c.risk c.reciprocity svo_angle || subject_id: , var


/// RC: knowledge and experience
meqrlogit choice_RI i.inv_cost ///
i.gt_knowledge i.lab_experience c.exp1 || subject_id: , var

meqrlogit choice_CA i.inv_cost ///
i.gt_knowledge i.lab_experience c.exp1 || subject_id: , var

meqrlogit decision_coop i.condition i.inv_cost ///
i.gt_knowledge i.lab_experience c.exp1 || subject_id: , var

/// RC: session characteristics 
meqrlogit choice_RI i.inv_cost ///
session_size i.RepeatFirst || subject_id: , var

meqrlogit choice_CA i.inv_cost ///
session_size i.RepeatFirst || subject_id: , var

meqrlogit decision_coop i.condition i.inv_cost ///
session_size i.RepeatFirst || subject_id: , var

/// RC: all together
meqrlogit choice_RI i.inv_cost ///
c.age i.gender_num i.international c.trust c.risk c.reciprocity i.prosocial ///
i.gt_knowledge i.lab_experience c.exp1 session_size i.RepeatFirst || subject_id: , var

meqrlogit choice_CA i.inv_cost ///
c.age i.gender_num i.international c.trust c.risk c.reciprocity i.prosocial ///
i.gt_knowledge i.lab_experience c.exp1 session_size i.RepeatFirst || subject_id: , var

meqrlogit decision_coop i.condition i.inv_cost ///
c.age i.gender_num i.international c.trust c.risk c.reciprocity i.prosocial ///
i.gt_knowledge i.lab_experience c.exp1 session_size i.RepeatFirst || subject_id: , var

///////////////
//decisions under forced choices
gen forced_choice = 0
replace forced_choice = 1 if Treatment<=4 & choice==1 & condition==1
replace forced_choice = 2 if Treatment>=5 & choice==1 & condition==2

// numbers for footnote on forced play
tab forced_choice part3

melogit choice_RI i.inv_cost  c.payoff_p2_cntr c.mechexp_p2_cntr part3 if forced_choice < 1 || subject_id:

melogit choice_CA i.inv_cost  c.payoff_p2_cntr c.mechexp_p2_cntr part3 if forced_choice < 1 || subject_id:

melogit decision_coop i.condition i.inv_cost if forced_choice < 1 || subject_id:

//==============Exogenous===============

// 0 = cooperate, 1 = defect
// cooperation exo one-shot
tab decision_EXO_ne if Period==1 & Treatment==1
// cooperation exo repeated
tab decision_EXO_ri if Period==1 & Treatment==1
// cooperation exo contract
tab decision_EXO_ic if Period==1 & Treatment==1


// cooperation endo one-shot
tab decision_coop if Period==1 & condition==1
// cooperation endo repeated
tab decision_coop if Period==1 & condition==2
// cooperation endo contract
tab decision_coop if Period==1 & condition==3


expand 4 if Treatment==1 & Period==1
bys session Treatment Subject Period: gen expander = _n
gen exo = (expander>1)
replace condition = 1 if expander == 2
replace decision_coop = 1-decision_EXO_ne if expander==2

replace condition = 2 if expander == 3
replace decision_coop = 1-decision_EXO_ri if expander==3

replace condition = 3 if expander == 4
replace decision_coop = 1-decision_EXO_ic if expander==4

// Table E.1
bys condition exo: tab decision_coop if Period==1

gen condition1exo = (condition==1 & exo==1)
gen condition2exo = (condition==2 & exo==1)
gen condition3exo = (condition==3 & exo==1)


// regression for Appendix Table E.2
melogit decision_coop i.condition condition1exo condition2exo condition3exo || subject_id:

